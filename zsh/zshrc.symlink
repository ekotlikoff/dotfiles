export DOT=$HOME/.dotfiles
export CORPDOT=$HOME/.corpdotfiles

dotenv="$DOT/.dotenv"
set -a
source "$dotenv" 2>/dev/null
set +a

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files (not including anything in */plugin/*)
typeset -U config_files
config_files=($DOT/**/*.zsh)
config_files=(${config_files:#*/plugin/*})

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

autoload -U compinit
compinit

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

if [[ "$DOT_TYPE" == "corp" ]]; then
  corp_config_files=($CORPDOT/**/*.zsh)
  corp_config_files=(${corp_config_files:#*/plugin/*})
  for file in ${corp_config_files}
  do
    source $file
  done
  unset corp_config_files
fi

# unset ^[/ (after compinit sets it) to allow for quickly using ^[ to exit insert mode and / to reverse history search
bindkey -M viins -r "^[/"

for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
